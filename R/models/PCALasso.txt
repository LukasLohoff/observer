model{
  # hierarchically pooled data model sqrt(precision)
  for(i in 1:t){
    s_inv[i] ~ dlnorm(mu_s_inv, tau2_s)                # pooling prior for data model sqrt(precision)
    s2[i] <- pow(s_inv[i], -2)                         # data model precision
  }
  mu_s_inv ~ dnorm(0, 1 / 1)                         # pooled mean for data model sqrt(precision)
  tau_s ~ dunif(0, 1)                                # pooled sqrt(precision) for data model sqrt(precision)
  # mu_s_inv ~ dnorm(0, 1 / 100)                         # pooled mean for data model sqrt(precision)
  # tau_s ~ dunif(0, 100)                                # pooled sqrt(precision) for data model sqrt(precision)
  tau2_s <- pow(tau_s, 2)                              # pooled precision for data model sqrt(precision)
  
  for (i in 1:N_total){
    mu_y[i] <- X[H[i], ] %*% beta[, tt[i]]            # data model mean for sparse data format
                                                      # X <- PCA rotation matrix 
                                                      # H <- observation indicator
  }

  # lasso priors
  for(j in 1:t){  
    lambda2[j] ~ dgamma(alpha_lambda2, beta_lambda2)  # hierarchically pooled lasso shrinkage parameter
    for(i in 1:p){
      gamma2[i, j] ~ dexp(lambda2[j] / 2)             # lasso scale mixture parameter
      beta[i, j] ~ dnorm(0, 1 / (s2[j] * gamma2[i, j] * D[i]))  # lasso prior model for PCA 
                                                      # D is a vector of singular values from PCA
    }
  }
  mu_lambda2 ~ dlnorm(0, 1 / 1)                       # hierarchical mean of lambda2 gamma distribution
  s_lambda2 ~ dunif(0, 1)                            # hierarchical standard deviation of lambda2 gamma distribution
  # mu_lambda2 ~ dlnorm(0, 1 / 100)                       # hierarchical mean of lambda2 gamma distribution
  # s_lambda2 ~ dunif(0, 100)                            # hierarchical standard deviation of lambda2 gamma distribution
  alpha_lambda2 <- pow(mu_lambda2, 2) / pow(s_lambda2, 2) # reparameterization of gamma distribution
  beta_lambda2 <- mu_lambda2 / pow(s_lambda2, 2)       # reparameterization of gamma distribution

  # likelihood
  for (i in 1:N_total){
      Y[i] ~ dnorm(mu_y[i], 1 / s2[tt[i]])           # data model for sparse data format
  }
}
