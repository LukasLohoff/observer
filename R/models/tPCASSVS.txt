model{
  # hierarchically pooled data model sqrt(precision)
  for(i in 1:t){
    s_inv[i] ~ dlnorm(mu_s_inv, tau2_s)                # pooling prior for data model sqrt(precision)
    s2[i] <- pow(s_inv[i], -2)                         # data model precision
  }
  mu_s_inv ~ dnorm(0, 1 / 1)                         # pooled mean for data model sqrt(precision)
  tau_s ~ dunif(0, 1)                                # pooled sqrt(precision) for data model sqrt(precision)
  # mu_s_inv ~ dnorm(0, 1 / 100)                         # pooled mean for data model sqrt(precision)
  # tau_s ~ dunif(0, 100)                                # pooled sqrt(precision) for data model sqrt(precision)
  tau2_s <- pow(tau_s, 2)                              # pooled precision for data model sqrt(precision)
  
  for (i in 1:N_total){
    mu_y[i] <- X[H[i], ] %*% beta[, tt[i]]             # data model mean for sparse data format
                                                       # X <- PCA rotation matrix
                                                       # H <- observation indicator
    v_inv[i] ~ dchisq(nu[tt[i]])                       # scale mixture for t data model variance
    v[i] <- (nu[tt[i]] * s2[tt[i]]) / v_inv[i]         # transformation to scaled inv-Chi squared variance
  }

  # hierarchically pooled ssvs priors
  for(i in 1:t){
    tau_beta[i] ~ dlnorm(mu_tau_beta, tau2_tau_beta)   # regression coefficient sqrt(precision)
    tau2_beta[i] <- pow(tau_beta[i], 2)                # regression coefficient precision
    precision_beta[1, i] <- tau2_beta[i]               # regression coefficient effectively zero
    precision_beta[2, i] <- tau2_beta[i] / 1000        # nonzero coefficient
  }
  mu_tau_beta ~ dnorm(0, 1 / 1)                           # pooled mean for regression coefficient sqrt(precision)
  tau_tau_beta ~ dunif(0, 1)                         # pooled sqrt(precision) for regression coefficient sqrt(precision)
  # mu_tau_beta ~ dnorm(0, 1 / 100)                           # pooled mean for regression coefficient sqrt(precision)
  # tau_tau_beta ~ dunif(0, 100)                         # pooled sqrt(precision) for regression coefficient sqrt(precision)
  tau2_tau_beta <- pow(tau_tau_beta, 2)                # pooled precision for  regression coefficient sqrt(precision)
  p_ind[1] <- 1/2                                      # prior probability of nonzero coefficient
  p_ind[2] <- 1 - p_ind[1]                             # prior probability effectively zero coefficient

  for (j in 1:t){
    for(i in 1:p){
      indA[i, j] ~ dcat(p_ind[])                       # sample indicator for nonzero coefficient, returns 1 or 2 
      gamma[i, j] <- indA[i, j] - 1                    # transform indicator from 0-1 to 1-2 for indexing, returns 0 or 1
      beta[i, j] ~ dnorm(0, precision_beta[indA[i, j], j] / D[i])  # ssvs prior for regression coefficients
    }
  }
  
  # t degrees of freedom
  for(i in 1:t){
    nu_inv[i] ~ dbeta(alpha_nu, beta_nu)               # pooled inverse degrees of freedom for t data model
    nu[i] <- 2 / nu_inv[i]                             # pooled degrees of freedom for t data model
  }

  mu_nu ~ dbeta(5, 5)                                  # prior for alternative parameterization
  eta_nu ~ dgamma(10, 0.1)                             # prior for alternative parameterization
  alpha_nu <- mu_nu * eta_nu                           # transformation of prior
  beta_nu <- (1 - mu_nu) * eta_nu                      # transformation of prior

  # likelihood
  for (i in 1:N_total){
    Y[i] ~ dnorm(mu_y[i], 1 / v[i])                    # t mixture data model using precision
    log_like[i] <- dnorm(Y[i], mu_y[i], 1 / v[i])      # t mixture data likelihood using precision
  }

}
